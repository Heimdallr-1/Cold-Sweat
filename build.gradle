buildscript {
    repositories {
        // These repositories are only for Gradle plugins, put any other repositories in the repository block further below
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'maven-publish'
    id 'java-library'
    id 'signing'
    id 'idea'
    id 'net.minecraftforge.gradle' version '5.1.+'
    //id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

group = group_id
version = mod_version

java {
    archivesBaseName = mod_name
    toolchain.languageVersion = JavaLanguageVersion.of(17)
    //withJavadocJar()
    withSourcesJar()
}

minecraft {
    mappings channel: 'official', version: '1.19.4'
    accessTransformer = file("src/main/resources/META-INF/coldsweat_at.cfg")
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'coldsweat'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            mods {
                coldsweat {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'coldsweat'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            mods {
                coldsweat {
                    source sourceSets.main
                }
            }
        }

        gameTestServer {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'coldsweat'
            mods {
                coldsweat {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'cold_sweat', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                cold_sweat {
                    source sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, "cold_sweat.mixin-refmap.json"
    config 'cold_sweat.mixin.json'
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url 'https://www.cursemaven.com'
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        name = "Illusive Soulworks maven"
        url = "https://maven.theillusivec4.top/"
    }
    mavenCentral()
}

dependencies {
    implementation 'com.github.t9t.minecraft-rcon-client:minecraft-rcon-client:1.0.0'
    minecraft 'net.minecraftforge:forge:1.19.4-45.2.2'
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    compileOnly fg.deobf("curse.maven:sereneapi-291874:4037228")
    compileOnly fg.deobf("curse.maven:werewolves-become-a-beast-417851:4570404")
    compileOnly fg.deobf("curse.maven:vampirism-become-a-vampire-233029:4649974")
    compileOnly fg.deobf("curse.maven:create-328085:4550987")
    compileOnly fg.deobf("curse.maven:armorunderwear-310383:4625067")
    compileOnly fg.deobf("curse.maven:carrots-311327:4624988")
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.19.4-5.1.5.4:api")

    //runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.19.4-5.1.5.4")
    runtimeOnly fg.deobf("curse.maven:spark-361579:4505309")
    //runtimeOnly fg.deobf("curse.maven:byg-247560:4602199")
    runtimeOnly fg.deobf("curse.maven:serene-seasons-291874:4577599")
    runtimeOnly fg.deobf("curse.maven:biomesoplenty-220318:4681520")
    runtimeOnly fg.deobf("curse.maven:terrablender-563928:4629898")
    //runtimeOnly fg.deobf("curse.maven:atmospheric-362393:4415516")
    //runtimeOnly fg.deobf("curse.maven:tough-as-nails-246391:3871450")
    //runtimeOnly fg.deobf("curse.maven:environmental-388992:4415514") // Causes weird issues IDK
    //runtimeOnly fg.deobf("curse.maven:blueprint-382216:4442628")
    runtimeOnly fg.deobf("curse.maven:jei-238222:4712556")
    //runtimeOnly fg.deobf("curse.maven:geckolib-388172:4568326")
    //runtimeOnly fg.deobf("curse.maven:corgilib-693313:4583678")
    //runtimeOnly fg.deobf("curse.maven:terralith-513688:4720177")
    runtimeOnly fg.deobf("curse.maven:wythers-490686:4454950")
    runtimeOnly fg.deobf("curse.maven:smooth-boot-633412:4685317")
    runtimeOnly fg.deobf("curse.maven:modernfix-790626:4728404")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'coldsweat'
            pom {
                name = 'Cold Sweat'
                description = 'A Minecraft temperature mod with a powerful framework'
                url = 'https://github.com/Momo-Softworks/Cold-Sweat'
                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    }
                }
                developers {
                    developer {
                        id = 'mikul'
                        name = 'Mikul'
                    }
                }
                /*scm = {
                    connection = 'scm:git:git://github.com/Momo-Softworks/Cold-Sweat.git'
                    developerConnection = 'scm:git:ssh://github.com/Momo-Softworks/Cold-Sweat.git'
                    url = 'https://github.com/Momo-Softworks/Cold-Sweat'
                }*/
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("SONATYPE_USERNAME")
                password = System.getenv("SONATYPE_PASSWORD")
            }
        }
    }
}

// Example for how to get properties into the manifest for reading at runtime.
jar {
    manifest {
        attributes([
                "Specification-Title"     : "cold_sweat",
                "Specification-Vendor"    : "Mikul",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "Mikul",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "TweakerClass"            : "org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder"              : 0,
                "MixinConfigs"            : "cold_sweat.mixin.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
